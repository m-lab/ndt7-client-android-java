apply plugin: "com.android.library"
apply plugin: "maven-publish"

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        consumerProguardFiles "proguard-rules.pro"

        // AGP 4.1+ doesn't add VERSION_NAME or VERSION_CODE to aars by default anymore
        def version =  '\"' + versionName.toString() +'\"'
        buildConfigField 'String', 'NDT7_ANDROID_VERSION_NAME', version

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            def flavor = variant.name
            def versionName = defaultConfig.versionName
            outputFileName = "${archivesBaseName}-${flavor}-${versionName}.aar"
        }
    }

}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation 'com.google.protobuf:protobuf-java:3.15.6'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId "net.measurementlab.ndt7.android"
            artifactId "libndt7"
            version project.android.defaultConfig.versionName
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/libndt7-debug-" + project.android.defaultConfig.versionName + ".aar")
        }
    }
}